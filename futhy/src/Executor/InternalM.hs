module Executor.InternalM where

import Types
import System.IO (stderr, hPrint, openTempFile, hClose)
import System.Process (readProcessWithExitCode, showCommandForUser)
import System.FilePath (dropExtension)
import Control.Monad.Reader
import Control.Monad.Except (throwError)
import GHC.IO.Exception (ExitCode(..))

p :: String -> Command ()
p = liftIO . hPrint stderr

-- |Compile the Futhark source code in env.
compile :: Command CommandResult
compile = do
  Env filepath backend <- ask
  let futExec = "futhark"
  let futParams = [show backend, filepath]
  p $ "[Futhark] Command going to be run: " ++ showCommandForUser futExec futParams

  output <- liftIO $ readProcessWithExitCode futExec futParams ""
  let (exitcode, stdout, stdin) = output
  case exitcode of
         ExitFailure _ -> throwError (CompilationError output)
         ExitSuccess   -> return ()

  p   "[Futhark] Compilation results:"
  p $ "[Futhark] ExitCode: " ++ show exitcode
  p $ "[Futhark] stdout:   " ++ show stdout
  p $ "[Futhark] stdin :   " ++ show stdin
  p   "[Futhark] Compilation COMPLETED"
  return $ Output output

-- |Execute the compiled Futhark executable 'futExec' containing the compiled linear program.
execute :: Command CommandResult
execute = do
  filepath <- asks fp
  let executable = dropExtension filepath
  let params = []
  p $ "[LinPgm] Command going to be run: " ++ showCommandForUser executable params

  output <- liftIO $ readProcessWithExitCode executable params "\n"
  let (exitcode, stdout, stdin) = output
  case exitcode of
         ExitFailure _ -> throwError (ExecutionError output)
         ExitSuccess   -> return ()

  p   "[LinPgm] Execution results:"
  p $ "[LinPgm] ExitCode: " ++ show exitcode
  p $ "[LinPgm] stdout:   " ++ show stdout
  p $ "[LinPgm] stdin :   " ++ show stdin
  p   "[LinPgm] Execution ENDED"
  return $ Output output

runFileM :: Command CommandResult
runFileM = compile >> execute

makeTemp :: Command FutPgmFile
makeTemp = do
  let path   = "build/"
  let prefix = "autogenerated_.fut"
  (filepath, handle) <- liftIO $ openTempFile path prefix
  liftIO $ hClose handle
  p filepath
  return filepath

writeTemp :: FutPgmStr -> Command ()
writeTemp futStr = do
  filepath <- asks fp
  liftIO $ writeFile filepath futStr

store :: FutPgmStr -> Command FutPgmFile
store futPgmStr = do
  filepath <- makeTemp
  backend <- asks be
  let envNew = Env { fp = filepath, be = backend }
  local (const envNew) (writeTemp futPgmStr)
  return filepath

runStrM :: FutPgmStr -> Command CommandResult
runStrM futPgmStr = do
  filepath <- store futPgmStr
  backend <- asks be
  let envNew = Env { fp = filepath, be = backend }
  local (const envNew) runFileM

runStr :: FutPgmStr -> Backend -> IO (Either CommandError CommandResult)
runStr futPgmStr backend =
  let envInit = Env { fp = "", be = backend }
  in execCmd (runStrM futPgmStr) envInit

runFile :: FutPgmFile -> Backend -> IO (Either CommandError CommandResult)
runFile futPgmFile backend =
  let envInit = Env { fp = futPgmFile, be = backend }
  in execCmd runFileM envInit
